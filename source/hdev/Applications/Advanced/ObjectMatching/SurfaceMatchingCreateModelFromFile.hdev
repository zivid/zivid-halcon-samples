<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.2" halcon_version="19.11.0.0">
<procedure name="main">
<interface/>
<body>
<c>* </c>
<c>* Example for the usage of a Zivid 3D camera in HALCON</c>
<c>* </c>
<c>* This example shows how to create a model for HALCON surface-based 3D</c>
<c>* matching algorithm. It should be used together with the</c>
<c>* SurfaceMatchingFindModelFromFile.hdev where the model created</c>
<c>* by this program is utilized for surface-based 3D matching algorithm.</c>
<c>* This example comes with models of three following objects:a Twinings</c>
<c>* tea box, a Pringles can (190 g), and a plastic Coca-Cola bottle (0.5 l).</c>
<c>* </c>
<c>* Please note that you first need to install Zivid Software and</c>
<c>* correctly set the environment variables. After this, you can</c>
<c>* access the camera with the HALCON GenICamTL interface.</c>
<c>*</c>
<c></c>
<c>* Preparing the graphics window</c>
<l>dev_close_window ()</l>
<l>WinWidth := 1920</l>
<l>WinHeight := 1200</l>
<l>dev_open_window (0, 0, WinWidth, WinHeight, 'black', Window3D)</l>
<c></c>
<c>* Reading a 3D point cloud of the scene</c>
<c>* Twinings tea box</c>
<l>Filename:= 'TwiningsTeaBox'</l>
<c>* Pringles can</c>
<l>* Filename:= 'PringlesCan'</l>
<c>* Coca-Cola bottle</c>
<l>* Filename:= 'CocaColaBottle'</l>
<l>ScenePath := environment('ZIVID_DATA') + '/ObjectMatching/' + Filename + '/Im1.ply'</l>
<l>read_object_model_3d (ScenePath, 'm', ['xyz_map_width'], [1920], ObjectModel3D_Scene, Status)</l>
<c>    </c>
<c>* Fixing to make the point cloud appear with colors</c>
<l>get_object_model_3d_params (ObjectModel3D_Scene, 'red', red)</l>
<l>get_object_model_3d_params (ObjectModel3D_Scene, 'green', green)</l>
<l>get_object_model_3d_params (ObjectModel3D_Scene, 'blue', blue)</l>
<l>set_object_model_3d_attrib (ObjectModel3D_Scene, '&amp;red', 'points', red, ObjectModel3D_Scene)</l>
<l>set_object_model_3d_attrib (ObjectModel3D_Scene, '&amp;green', 'points', green, ObjectModel3D_Scene)</l>
<l>set_object_model_3d_attrib (ObjectModel3D_Scene, '&amp;blue', 'points', blue, ObjectModel3D_Scene)</l>
<c>    </c>
<c>* Generating X, Y, and Z image</c>
<l>object_model_3d_to_xyz( X, Y, Z, ObjectModel3D_Scene, 'from_xyz_map', [], [])  </l>
<c>   </c>
<c>* Generating R, G, B, and RGB image</c>
<l>get_image_size (X, Width, Height)</l>
<l>gen_image_const (R, 'real', Width, Height)</l>
<l>gen_image_const (G, 'real', Width, Height)</l>
<l>gen_image_const (B, 'real', Width, Height)</l>
<l>get_domain (R, Domain)</l>
<l>get_region_points (Domain, Rows, Columns)</l>
<l>set_grayval (R, Rows, Columns, red)</l>
<l>set_grayval (G, Rows, Columns, green)</l>
<l>set_grayval (B, Rows, Columns, blue)</l>
<l>compose3(R,G,B,RGB)</l>
<c>    </c>
<c>* Use the mouse cursor to draw a region around the object</c>
<l>dev_disp_text (['Position the mouse cursor on the edge of the object', 'Click and hold the left mouse button to begin drawing the ROI around the object', 'Release the left mouse button to end drawing the ROI', 'Click the right mouse button to continue running the program'], 'window', 'bottom', 'left', 'black', [], [])</l>
<l>draw_region (RegionDraw, Window3D)</l>
<c></c>
<c>* Geting min and max Z values encapsulated by the created region</c>
<l>min_max_gray (RegionDraw, Z, 0, MinZ, MaxZ, RangeZ)</l>
<c></c>
<c>* Segmenting the Z image with Z values encapsulated by the created region</c>
<l>threshold (Z, RegionZ, MinZ, MaxZ)</l>
<l>reduce_domain (Z, RegionZ, Z)</l>
<c></c>
<c>* Segmenting the resulting Z image with the created region</c>
<l>reduce_domain (Z, RegionDraw, Z)</l>
<c></c>
<c>* Transforming 3D points from X, Y, Z images to a HALCON 3D object model</c>
<l>xyz_to_object_model_3d (X, Y, Z, ObjectModel3D)</l>
<c></c>
<c>* Computing normals for the HALCON 3D object model</c>
<l>count_seconds (T0)</l>
<l>sample_object_model_3d(ObjectModel3D,'fast_compute_normals', 1, [], [], ObjectModel3D)</l>
<l>count_seconds (T1)</l>
<l>TimeForSampling := (T1 - T0)</l>
<c></c>
<c>* Creating a HALCON surface-based matching model</c>
<l>count_seconds (T0)</l>
<l>create_surface_model (ObjectModel3D, 0.01, [], [], SFM)</l>
<l>count_seconds (T1)</l>
<l>TimeForCreating := (T1 - T0)</l>
<c></c>
<c>* Saving the HALCON 3D object model</c>
<l>write_object_model_3d(ObjectModel3D, 'om3', 'ObjectModel3D', [], [] )</l>
<c></c>
<c>* Saving the HALCON surface-based matching model</c>
<l>write_surface_model(SFM,'SFM')</l>
<c></c>
<c>* Loading the HALCON 3D object model</c>
<l>read_object_model_3d( 'ObjectModel3D', 'm', [], [], ObjectModel3D, Status)</l>
<c></c>
<c>* Loading the HALCON surface-based matching model</c>
<l>read_surface_model('SFM', SFM)</l>
<c></c>
<c>* Displaying the 3D color point cloud, press the button to continue</c>
<l>Instructions[0] := 'Rotate: Left button'</l>
<l>Instructions[1] := 'Zoom:   Shift + left button'</l>
<l>Instructions[2] := 'Move:   Ctrl  + left button'</l>
<l>visualize_object_model_3d (Window3D, ObjectModel3D, [], [], [], [], [], [], [], PoseOut)</l>
<c></c>
<c>* Closing the graphics window</c>
<l>dev_set_window (Window3D)</l>
<l>dev_close_window ()</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
