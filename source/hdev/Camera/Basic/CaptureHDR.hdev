<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.2" halcon_version="19.05.0.0">
<procedure name="main">
<interface/>
<body>
<c>* </c>
<c>* Example for the usage of a Zivid 3D camera in HALCON</c>
<c>* </c>
<c>* This example shows how to acquire an HDR image from the camera</c>
<c>* and use it to generate a HALCON ObjectModel3D which is then</c>
<c>* visualized.</c>
<c>* </c>
<c>* Please note that you first need to install Zivid Software and</c>
<c>* correctly set the environment variables. After this, you can</c>
<c>* access the camera with the HALCON GenICamTL interface.</c>
<c>* </c>
<c></c>
<c>* Preparing graphics windows</c>
<l>dev_close_window ()</l>
<l>WinWidth := 960</l>
<l>WinHeight := 800</l>
<l>dev_open_window (0, 0, WinWidth, WinHeight, 'blue', Window3D)</l>
<l>dev_open_window (0, WinWidth, WinWidth, WinHeight, 'black', Window2D)</l>
<c></c>
<c>*Get first available Zivid device</c>
<l>info_framegrabber ('GenICamTL','device', Information, Devices)</l>
<l>import './../../Procedures'</l>
<l>get_first_available_zivid_device (Devices, Device)</l>
<c></c>
<c>* Connecting to the Zivid camera</c>
<l>open_framegrabber ('GenICamTL',1, 1, 0, 0, 0, 0, 'progressive', -1, 'default', -1, 'false', 'default', Device, 0, 0, AcqHandle)</l>
<c></c>
<c>* Configuring 3D-settings</c>
<l>set_framegrabber_param (AcqHandle, 'create_objectmodel3d', 'enable')</l>
<l>set_framegrabber_param (AcqHandle, 'add_objectmodel3d_overlay_attrib', 'enable')</l>
<c></c>
<c>* Seting the timeout for aborting a pending grab to infinity</c>
<l>set_framegrabber_param (AcqHandle, 'grab_timeout', -1)</l>
<c></c>
<c>* Switching to the HDR mode</c>
<l>set_framegrabber_param (AcqHandle, 'AcquisitionMode', 'MergedMultiFrame')</l>
<c></c>
<c>* Reseting any previous HDR settings</c>
<l>set_framegrabber_param (AcqHandle, 'MergedMultiFrameReset', 1)</l>
<c></c>
<c>* Configuring HDR settings</c>
<l>set_framegrabber_param (AcqHandle, 'MergedMultiFrameMode', 'HDR')</l>
<l>set_framegrabber_param (AcqHandle, 'MergedMultiFrameIris', 16)</l>
<l>set_framegrabber_param (AcqHandle, 'MergedMultiFrameAdd', '1')</l>
<l>set_framegrabber_param (AcqHandle, 'MergedMultiFrameIris', 20)</l>
<l>set_framegrabber_param (AcqHandle, 'MergedMultiFrameAdd', '1')</l>
<l>set_framegrabber_param (AcqHandle, 'MergedMultiFrameIris',50)</l>
<l>set_framegrabber_param (AcqHandle, 'MergedMultiFrameAdd', '1')</l>
<c></c>
<c>* Grabing the HDR image</c>
<l>grab_data (Image, Region, Contours, AcqHandle, ObjectModel3D)</l>
<c></c>
<c>* Extracting the following images: X, Y, Z, Contrast, and RGB</c>
<l>select_obj (Image, X, 1)</l>
<l>select_obj (Image, Y, 2)</l>
<l>select_obj (Image, Z, 3)</l>
<l>select_obj (Image, Confidence, 4)</l>
<l>select_obj (Image, RGBA, 5)</l>
<c></c>
<c>* Displaying the 2D color image</c>
<l>dev_set_window (Window2D)</l>
<l>dev_display (RGBA)</l>
<c></c>
<c>* Displaying the 3D color point cloud, press the button to continue</c>
<l>Instructions[0] := 'Rotate: Left button'</l>
<l>Instructions[1] := 'Zoom:   Shift + left button'</l>
<l>Instructions[2] := 'Move:   Ctrl  + left button'</l>
<l>dev_set_window (Window3D)</l>
<l>visualize_object_model_3d (Window3D, ObjectModel3D, [], [], ['red_channel_attrib','green_channel_attrib','blue_channel_attrib'], ['&amp;overlay_red','&amp;overlay_green','&amp;overlay_blue'], [], [], [Instructions], PoseOut)</l>
<c></c>
<c>* Disconnecting from the Zivid Camera</c>
<l>close_framegrabber (AcqHandle)</l>
<c></c>
<c>* Closing graphics windows</c>
<l>dev_set_window (Window2D)</l>
<l>dev_close_window ()</l>
<l>dev_set_window (Window3D)</l>
<l>dev_close_window ()</l>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
